plugins {
    id "java"
    id "io.franzbecker.gradle-lombok" version "5.0.0" apply false
    id "com.diffplug.spotless" version "6.25.0" apply false
    id "com.avast.gradle.docker-compose" version "0.17.6"
}

apply plugin: "com.diffplug.spotless"

dependencies {
    implementation("triplea:lobby-client:2.6.14756")
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/triplea-game/triplea"
        credentials {
            username = project.property("triplea.github.username")
            password = project.property("triplea.github.access.token")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.incremental = true
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat = "full"
        events = ["standardOut", "standardError", "skipped", "failed"]
    }
}

spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
    }
}
